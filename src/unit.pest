main_term = { "/" ~ term | term }

term = {
    dot_term |
        slash_term |
        basic_term
}

dot_term = { component ~ "." ~ term }
slash_term = { component ~ "/" ~ term }
basic_term = { component }

component = { component_with_factor | basic_component }

component_with_factor = { factor ~ basic_component }

basic_component = {
    annotated_annotatable |
        annotatable |
        annotation |
        factor |
        "(" ~ term ~ ")"
}

annotated_annotatable = { annotatable ~ annotation }
annotation = { "{" ~ annotation_string ~ "}" }
annotation_string = { ('!'..'z')* }
open_curly = _{ "{" }
closed_curly = _{ "}" }

annotatable = { simple_unit_with_exponent | simple_unit }
simple_unit_with_exponent = { simple_unit ~ exponent }
simple_unit = { prefixed_atom | atom_symbol }

prefixed_atom = { prefix_symbol ~ atom_symbol }

// Atoms
atom_symbol = {
    // Put base units first since they're most used.
    candela                 |
        coulomb                 |
        gram                    |
        kelvin                  |
        meter                   |
        radian                  |
        second                  |

        // Derived units
        acre_us                 |
        are                     |
        degree_celsius          |
        degree_fahrenheit       |
        degree_reaumur          |
        degree                  |
        fluid_ounce_us          |
        foot_international      |
        foot_us                 |
        gill_us                 |
        inch_international      |
        liter                   |
        mole                    |
        parts_per_billion       |
        parts_per_million       |
        parts_per_thousand      |
        percent                 |
        ph                      |
        pint_us                 |
        prism_diopter           |
        quart_us                |
        queen_annes_wine_gallon |
        rod_us                  |
        the_number_pi           |
        the_number_ten_for_arbitrary_powers_caret |
        the_number_ten_for_arbitrary_powers_star
}

// Order: primary_code | secondary_code | print_symbol (if any) | name(s)
candela                 = { "cd" | "CD" | "candela" }
coulomb                 = { "C" | "Coulomb" }
gram                    = { "g" | "G" | "gram" }
kelvin                  = { "K" | "Kelvin" }
meter                   = { "m" | "M" | "meter" }
radian                  = { "rad" | "RAD" | "radian" }
second                  = { "s" | "S" | "second" }

acre_us                 = { "[acr_us]" | "[ACR_US]" | "acre" }
are                     = { "ar" | "AR" | "a" | "are" }
degree                  = { "deg" | "DEG" | "°" | "degree" }
degree_celsius          = { "Cel" | "CEL" | "°C" | "degree Celsius" }
degree_fahrenheit       = { "[degF]" | "[DEGF]" | "°F" | "degree Fahrenheit" }
degree_reaumur          = { "[degRe]" | "°Ré" | "degree Réaumur" }
fluid_ounce_us          = { "[foz_us]" | "[FOZ_US]" | "oz fl" | "fluid ounce" }
foot_international      = { "[ft_i]" | "[FT_I]" | "ft" | "foot" }
foot_us                 = { "[ft_us]" | "[FT_US]" | "ft(us)" }
gill_us                 = { "[gil_us]" | "[GIL_US]" | "gill" }
inch_international      = { "[in_i]" | "[IN_I]" | "in" | "inch" }
liter                   = { "l" | "L" | "liter" }
mole                    = { "mol" | "MOL" | "mole" }
parts_per_billion       = { "[ppb]" | "[PPB]" | "ppb" | "parts per billion" }
parts_per_million       = { "[ppm]" | "[PPM]" | "ppm" | "parts per million" }
parts_per_thousand      = { "[ppth]" | "[PPTH]" | "ppt" | "parts per thousand" }
percent                 = { "%" | "percent" }
ph                      = { "[pH]" | "[PH]" | "pH" }
pint_us                 = { "[pt_us]" | "[PT_US]" | "pint" }
prism_diopter           = { "[p'diop]" | "[P'DIOP]" | "PD" | "prism diopter" }
quart_us                = { "[qt_us]"| "[QT_US]" | "quart" }
queen_annes_wine_gallon = { "[gal_us]" | "[GAL_US]" | "Queen Anne's wine gallon" }
rod_us                  = { "[rd_us]" | "[RD_US]" | "rod" }
the_number_pi           = { "[pi]" | "[PI]" | "π" | "the number pi" }
the_number_ten_for_arbitrary_powers_caret = { "10^" }
the_number_ten_for_arbitrary_powers_star = { "10*" }

// Prefixes
prefix_symbol = {
    atto  |
        centi |
        deci  |
        deka  |
        exa   |
        femto |
        gibi  |
        giga  |
        hecto |
        kilo  |
        mebi  |
        mega  |
        micro |
        milli |
        nano  |
        peta  |
        tebi  |
        tera  |
        yocto |
        zepto |
        zetta |
        yotta
}

atto  = { "a"  | "A"       | "atto" }
centi = { "c"  | "C"       | "centi" }
deci  = { "d"  | "D"       | "deci" }
deka  = { "da" | "DA"      | "deka" }
exa   = { "E"  | "EX"      | "exa" }
femto = { "f"  | "F"       | "femto" }
gibi  = { "Gi" | "GIB"     | "gibi" }
giga  = { "G"  | "GA"      | "giga" }
hecto = { "h"  | "H"       | "hecto" }
kilo  = { "k"  | "K"       | "kilo" }
mebi  = { "Mi" | "MIB"     | "mebi" }
mega  = { "M"  | "MA"      | "mega" }
micro = { "u"  | "U"       | "µ" | "micro" }
milli = { "m"  | "milli" }
nano  = { "n"  | "N"       | "nano" }
peta  = { "P"  | "PT"      | "peta" }
tebi  = { "Ti" | "TIB"     | "tebi" }
tera  = { "T"  | "TR"      | "tera" }
yocto = { "y"  | "YO"      | "yocto" }
yotta = { "Y"  | "YA"      | "yotta" }
zepto = { "z"  | "ZO"      | "zepto" }
zetta = { "Z"  | "ZA"      | "zetta" }


exponent = { sign? ~ digits }
factor = { digits }
digits = @{ digit+ }
digit = { '0'..'9' }
sign = { "+" | "-" }

// vim:ft=rust
