// <main-term>
main_term = ${ slash ~ term | term }

// <term>
term = {
    component ~ "." ~ term | component ~ slash ~ term | component
}

slash = { "/" }

// <component>
component = { factor ~ basic_component | basic_component }

basic_component = {
    annotatable ~ annotation |
        annotatable |
        annotation |
        factor |
        "(" ~ term ~ ")"
}

// <annotation>
annotation = { "{" ~ ('!'..'z')* ~ "}" }

// <annotatable>
annotatable = { simple_unit ~ exponent | simple_unit }

// <simple-unit>
simple_unit = { prefix_symbol ~ atom_symbol | atom_symbol }

// Atoms
atom_symbol = {
     "atm"   | "a_g" | "a_j" | "a_t" |
        "ar"        | "a"   |
        "ANN_G"     | "ANN_J" | "ANN_T" |
        "AMU"       | "ANN"   | "ASU"   | "ATM" |
        "AR"        | "AU"    | "A"     |
        "bar"   |
        "BAR"       | "Bi"    | "Bq"    | "BI"  | "BQ"  |
        "cd"        |
        "Cel"       | "CEL"   | "Ci"    | "CD"  | "CI"  | "C"   |

        "deg" | "dyn" | "d"  |
        "DEG"    | "DYN" | "D"   |
        "erg"    | "eV"  | "eq"  | "ERG" | "EV" | "EQ" |
        "ft"  |
        "F"      |

        "gon"   | "gf"  | "g%"  | "g"   |
        "GAL"       | "GON"   |
        "Gb"        | "Gy"    | "GB"    | "GF"  | "GL"  | "GS"  | "GY" |
        "G"         |
        "h"     |
        "Hz"        | "HR"    | "HZ"    | "H"   |
        "in"    |

        "J"         |
        "Ky"        | "KY"    | "K"     |
        "l.y."  | "lbf"   | "lm"  | "lx"  | "l"   |
        "Lmb"       | "LMB"   | "LM"    | "LX"  | "L"   |

        "mo_g"      | "mo_j"  | "mo_s"  |
        "min"       | "mol"   |
        "mo"        | "m"     |
        "MO_G"      | "MO_J"  | "MO_S"  |
        "MIN"       | "MOL"   | "Mx"    | "MO"  | "MX"  | "M"   |
        "N"         |
        "Ohm"       | "OHM"   | "Oe"    | "OE"  |

        "ppb"   | "ppm"   | "ppt" | "pc"  | "pH"  | "ph" |
        "PAL"       | "PHT"   | "PRS"   | "Pa"  | "PD"  | "P"   |
        "rad"       | "RAD"   | "REM"   | "ROE"   | "R"   |

        "sb"    | "sr"    | "s"   |
        "SIE"       | "St"    | "Sv"    | "SB"  | "ST"  | "SV"  | "SR" |
        "S"         |
        "t"     |
        "TNE"       | "T"     |
        "u"         |

        "V"     |
        "wk"    |
        "Wb"        | "WB"    | "WK"    | "W"   |

        "[acr_br]" | "[ACR_BR]" | "[acr_us]" | "[ACR_US]" |
        "[bbl_us]" | "[bu_br]"  | "[bu_us]"  | "[bf_i]"   |
        "[BBL_US]" | "[BU_BR]"  | "[BU_US]"  | "[BF_I]"   |
        "[cft_i]"  | "[cin_i]"  | "[cml_i]"  | "[crd_us]" | "[cup_us]" | "[cup_m]" | "[cyd_i]" |
        "[ch_br]"  | "[ch_us]"  | "[cr_i]"   | "[c]"      |
        "[CFT_I]"  | "[CIN_I]"  | "[CML_I]"  | "[CRD_US]" | "[CUP_US]" | "[CUP_M]" | "[CYD_I]" |
        "[CH_BR]"  | "[CH_US]"  | "[CR_I]"   | "[C]"      |

        "[dpt_us]" | "[dqt_us]" | "[degF]"   | "[degRe]"  | "[dr_av]"  |
        "[DPT_US]" | "[DQT_US]" | "[DEGF]"   | "[DR_AV]"  |
        "[eps_0]"  | "[e]"      |
        "[EPS_0]"  | "[E]"      |
        "[fdr_br]" | "[fdr_us]" | "[foz_br]" | "[foz_us]" | "[fth_br]" | "[fth_us]" | "[fur_us]" |
        "[foz_m]"  | "[fth_i]"  | "[ft_br]"  | "[ft_us]"  | "[ft_i]"   |
        "[FDR_BR]" | "[FDR_US]" | "[FOZ_BR]" | "[FOZ_US]" | "[FTH_BR]" | "[FTH_US]" | "[FUR_US]" |
        "[FOZ_M]"  | "[FTH_I]"  | "[FT_BR]"  | "[FT_US]"  | "[FT_I]"   |

        "[gal_br]" | "[gal_us]" | "[gal_wi]" | "[gil_br]" | "[gil_us]" | "[gr]"   | "[g]"  |
        "[GAL_BR]" | "[GAL_US]" | "[GAL_WI]" | "[GIL_BR]" | "[GIL_US]" | "[GC]"   | "[GR]" | "[G]" |
        "[hd_i]"   | "[h]"      |
        "[HD_I]"   | "[HP]"     | "[H]"      |
        "[in_br]"  | "[in_us]"  | "[in_i]"   | "[IN_BR]"  | "[IN_US]"  | "[IN_I]" |

        "[kn_br]"   | "[kn_i]"    | "[k]"      |
        "[KN_BR]"   | "[KN_I]"    | "[K]"      |
        "[lcwt_av]" | "[lton_av]" | "[lbf_av]" |
        "[LCWT_AV]" | "[LTON_AV]" | "[LBF_AV]" |
        "[lb_av]"   | "[lb_tr]"   | "[lk_br]"  | "[lk_us]" | "[ly]" |
        "[LB_AV]"   | "[LB_TR]"   | "[LK_BR]"  | "[LK_US]" | "[LY]" |

        "[mil_us]" | "[min_br]" | "[min_us]" | "[mil_i]" | "[mi_br]" | "[mi_us]" |
        "[mi_i]"   | "[mu_0]"   | "[m_e]"    | "[m_p]"   |
        "[MIL_US]" | "[MIN_BR]" | "[MIN_US]" | "[MIL_I]" | "[MI_BR]" | "[MI_US]" |
        "[MI_I]"   | "[MU_0]"   | "[M_E]"    | "[M_P]"   |
        "[nmi_br]" | "[NMI_BR]" | "[nmi_i]"  | "[NMI_I]" |
        "[oz_av]"  | "[oz_tr]"  | "[OZ_AV]"  | "[OZ_TR]" |

        "[p'diop]" | "[pwt_tr]" | "[pc_br]"  | "[pk_br]" | "[pk_us]" | "[pt_br]" | "[pt_us]" |
        "[P'DIOP]" | "[PWT_TR]" | "[PC_BR]"  | "[PK_BR]" | "[PK_US]" | "[PT_BR]" | "[PT_US]" |
        "[ppth]"   | "[ppb]"    | "[ppm]"   | "[pH]"    | "[pi]"    |
        "[PPTH]"   | "[PNU]"    | "[PPB]"    | "[PPM]"   | "[PH]"    | "[PI]"    |
        "[qt_br]"  | "[QT_BR]"  | "[qt_us]" | "[QT_US]" |
        "[rch_us]" | "[rlk_us]" | "[rd_br]" | "[rd_us]" |
        "[RCH_US]" | "[RLK_US]" | "[RD_BR]" | "[RD_US]" | "[RAD]"   | "[REM]" |

        "[stone_av]" | "[scwt_av]" | "[ston_av]" | "[smi_us]" | "[srd_us]" |
        "[STONE_AV]" | "[SCWT_AV]" | "[STON_AV]" | "[SMI_US]" | "[SRD_US]" |
        "[sft_i]"    | "[sin_i]"   | "[syd_i]"   | "[sct]"    |
        "[SFT_I]"    | "[SIN_I]"   | "[SYD_I]"   | "[SCT]"    |
        "[tbs_us]"   | "[tsp_us]"  | "[tbs_m]"   | "[tsp_m]"  | "[twp]"    |
        "[TBS_US]"   | "[TSP_US]"  | "[TBS_M]"   | "[TSP_M]"  | "[TWP]"    |
        "[yd_br]"    | "[yd_us]"   | "[yd_i]"    | "[YD_BR]"  | "[YD_US]"  | "[YD_I]" |

        "10^"      |
        "10*"      |
        "1"        |
        "%"        |
        "'"        |
        "''"
        | atom_char+
}

// Prefixes
prefix_symbol = {
     "a"  | "A"   |
         "c"  | "C"   |
         "da" | "DA"  |
         "d"  | "D"   |
         "E"  | "EX"  |
         "f"  | "F"   |
         "Gi" | "GIB" |
         "G"  | "GA"  |
         "h"  | "H"   |
         "k"  | "K"   |
         "Mi" | "MIB" |
         "M"  | "MA"  |
         "u"  | "U"   |
         "m"  |
         "n"  | "N"   |
         "P"  | "PT"  |
         "Ti" | "TIB" |
         "T"  | "TR"  |
         "y"  | "YO"  |
         "Y"  | "YA"  |
         "z"  | "ZO"  |
         "Z"  | "ZA"
}

exponent = { sign? ~ digits }
factor = { '0'..'9'+ }
digits = { '0'..'9'+ }
sign = { "-" | "+" }

// vim:ft=rust
