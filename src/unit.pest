main_term = { slash_main_term | term }

slash_main_term =  { "/" ~ term }

term = {
    dot_term |
        slash_term |
        basic_term
}

dot_term = { component ~ "." ~ term }
slash_term = { component ~ "/" ~ term }
basic_term = { component }

component = { component_with_factor | basic_component }

component_with_factor = { factor ~ basic_component }

basic_component = {
    annotated_annotatable |
        annotatable |
        annotation |
        factor |
        "(" ~ term ~ ")"
}

annotated_annotatable = { annotatable ~ annotation }
annotation = { "{" ~ annotation_string ~ "}" }
annotation_string = { ('!'..'z')* }
open_curly = _{ "{" }
closed_curly = _{ "}" }

annotatable = { simple_unit_with_exponent | simple_unit }
simple_unit_with_exponent = { simple_unit ~ exponent }
simple_unit = { prefixed_atom | atom_symbol }

prefixed_atom = { prefix_symbol ~ atom_symbol }

// Atoms
atom_symbol = {
    "cd"  | "CD"  |
                    "C"   |
        "gram"    | "g"   | "G"   |
                    "K"   |
        "meter"   | "m"   | "M"   |
                    "rad" | "RAD" |
        "second"  | "s"   |

        "acre"                     | "[acr_us]" | "[ACR_US]" |
        "are"                      | "ar"       | "AR"       |
                                     "a_t"      | "ANN_T"    | "aₜ"    |
                                     "a_j"      | "ANN_J"    | "aⱼ"    |
                                     "a_g"      | "ANN_G"    |
        "year"                     | "a"        | "ANN"      |
                                     "u"        | "AMU"      |
                                     "AU"       | "ASU"      |
                                     "A"        |
                                     "bar"      | "BAR"      |
        "becquerel"                | "Bq"       | "BQ"       |
                                     "Cel"      | "CEL"      | "°C"    |
                                     "[degF]"   | "[DEGF]"   | "°F"    |
                                     "[degRe]"  | "°Ré"      |
        "degree"                   | "deg"      | "DEG"      | "°"     |
        "day"                      | "d"        | "D"        |
                                     "eV"       | "EV"       |
                                     "F"        |
                                     "[foz_us]" | "[FOZ_US]" | "oz fl" |
                                     "[ft_us]"  | "[FT_US]"  |
        "foot"                     | "[ft_i]"   | "[FT_I]"   | "ft"    |
                                     "[gil_us]" | "[GIL_US]" |
                                     "gon"      | "GON"      | "grade" |
                                     "Gy"       | "GY"       |
                                     "Hz"       | "HZ"       |
                                     "H"        |
        "hour"                     | "h"        | "HR"       |
        "inch"                     | "[in_i]"   | "[IN_I]"   | "in"    |
                                     "J"        |
                                     "lm"       | "LM"       |
                                     "lx"       | "LX"       |
        "liter"                    | "l"        | "L"        |
        "minute"                   | "min"      | "MIN"      |
                                     "mol"      | "MOL"      |
                                     "mo_g"     | "MO_G"     |
                                     "mo_j"     | "MO_J"     | "moⱼ"   |
                                     "mo_s"     | "MO_S"     | "moₛ"   |
        "month"                    | "mo"       | "MO"       |
                                     "N"        |
                                     "Ohm"      | "OHM"      | "Ω"     |
                                     "pc"       | "PRS"      |
                                     "Pa"       | "PAL"      |
                                     "[ppb]"    | "[PPB]"    | "ppb"   |
                                     "[ppm]"    | "[PPM]"    | "ppm"   |
                                     "[ppth]"   | "[PPTH]"   | "ppt"   |
        "pH"                       | "[pH]"     | "[PH]"     |
        "pint"                     | "[pt_us]"  | "[PT_US]"  |
                                     "[p'diop]" | "[P'DIOP]" | "PD"    |
        "quart"                    | "[qt_us]"  | "[QT_US]"  |
                                     "[gal_us]" | "[GAL_US]" |
                                     "[rd_us]"  | "[RD_US]"  |
                                     "Sv"       | "SV"       |
                                     "sr"       | "SR"       |
                                     "S"        | "SIE"      |
                                     "[pi]"     | "[PI]"     | "π"     |
        "tonne"                    | "t"        | "TNE"      |
                                     "T"        |
        "volt"                     | "V"        |
                                     "Wb"       | "WB"       |
        "watt"                     | "W"        |
        "week"                     | "wk"       | "WK"       |
                                     "%"        |
                                     "'"        |
                                     "''"       |
                                     "10^"      |
                                     "10*"      |
                                     "1"
}

// Prefixes
prefix_symbol = {
    "atto"  | "a"  | "A"   |
        "centi" | "c"  | "C"   |
        "deka"  | "da" | "DA"  |
        "deci"  | "d"  | "D"   |
        "exa"   | "E"  | "EX"  |
        "femto" | "f"  | "F"   |
        "gibi"  | "Gi" | "GIB" |
        "giga"  | "G"  | "GA"  |
        "hecto" | "h"  | "H"   |
        "kilo"  | "k"  | "K"   |
        "mebi"  | "Mi" | "MIB" |
        "mega"  | "M"  | "MA"  |
        "micro" | "u"  | "U"   | "µ" |
        "milli" | "m"  |
        "nano"  | "n"  | "N"   |
        "peta"  | "P"  | "PT"  |
        "tebi"  | "Ti" | "TIB" |
        "tera"  | "T"  | "TR"  |
        "yocto" | "y"  | "YO"  |
        "yotta" | "Y"  | "YA"  |
        "zepto" | "z"  | "ZO"  |
        "zetta" | "Z"  | "ZA"
}

exponent = { sign? ~ digits }
factor = { digits }
digits = @{ digit+ }
digit = { '0'..'9' }
sign = { "-" | "+" }

// vim:ft=rust
