use atom::Atom;
use atom::base::*;
use std::str::FromStr;
use parser_terms::*;
use prefix::{Prefix, PREFIXES};

grammar;

pub MainTerm: Term<'input> = {
    "/" <Term>,
    <Term>
};

pub Term: Term<'input> = {
    <c:Component> "." <t:Term> => Term::DotCombined(c, Box::new(t)),
    <c:Component> "/" <t:Term> => Term::SlashCombined(c, Box::new(t)),
    <Component> => Term::Basic(<>),
};

pub Component: Component<'input> = {
    <Annotatable><Annotation> => Component::AnnotatedAnnotatable(<>),
    <Annotatable> => Component::Annotatable(<>),
    <Annotation> => Component::Annotation(<>),
    <Factor> => Component::Factor(<>),
    "(" <Term> ")" => Component::Term(Box::new(<>)),
};

pub Annotation: Annotation<'input> = {
    "{" <s:r"[!-z|~]*\}"> => Annotation(s.trim_right_matches("}"))
};

pub Annotatable: Annotatable = {
    <SimpleUnit><Exponent> => Annotatable::UnitWithPower(<>),
    <SimpleUnit> => Annotatable::Unit(<>),
};

pub SimpleUnit: SimpleUnit = {
    <AtomSymbol> => SimpleUnit::Atom(<>),
    <PrefixSymbol><AtomSymbol> => SimpleUnit::PrefixedAtom(<>)
};

pub AtomSymbol: Box<Atom> = {
    <AtomByPrimaryCode>,
    <AtomBySecondaryCode>
};

AtomByPrimaryCode: Box<Atom> = {
    "m" => Box::new(Meter) as Box<Atom>,
    "s" => Box::new(Second) as Box<Atom>,
    "g" => Box::new(Gram) as Box<Atom>,
    "rad" => Box::new(Radian) as Box<Atom>,
    "K" => Box::new(Kelvin) as Box<Atom>,
    "C" => Box::new(Coulomb) as Box<Atom>,
    "cd" => Box::new(Candela) as Box<Atom>,
};

AtomBySecondaryCode: Box<Atom> = {
    "M" => Box::new(Meter) as Box<Atom>,
    "S" => Box::new(Second) as Box<Atom>,
    "G" => Box::new(Gram) as Box<Atom>,
    "RAD" => Box::new(Radian) as Box<Atom>,
    "CD" => Box::new(Candela) as Box<Atom>,
};

pub PrefixSymbol: Prefix = {
    <PrefixByPrimaryCode>,
    <PrefixBySecondaryCode>
};

PrefixByPrimaryCode: Prefix = {
    "k" => PREFIXES[7].clone()
};

PrefixBySecondaryCode: Prefix = {
    "K" => PREFIXES[7].clone()
};

pub Exponent: Exponent = {
    <Sign><Digits> => Exponent(<>),
    <d:Digits> => Exponent(UnitSign::Positive, d)
};

Factor: Factor = {
    <Digits> => Factor(<>),
};

Digits: u32 = {
    <s:r"[1-9]+[0-9]*">  => u32::from_str(s).unwrap()
};

Sign: UnitSign = { PositiveSign, NegativeSign };
PositiveSign: UnitSign = <s:"+"> => UnitSign::Positive;
NegativeSign: UnitSign = <s:"-"> => UnitSign::Negative;

// vi:ft=rust
