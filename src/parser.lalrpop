use atom::{Atom, ATOMS};
use std::str::FromStr;
use parser_terms::*;
use prefix::{Prefix, PREFIXES};

grammar;

pub Term: Term<'input> = {
    <c:Component> "." <t:Term> => Term::DotCombined(c, Box::new(t)),
    <c:Component> "/" <t:Term> => Term::SlashCombined(c, Box::new(t)),
    <Component> => Term::Basic(<>),
};

pub Component: Component<'input> = {
    <Annotatable><Annotation> => Component::AnnotatedAnnotatable(<>),
    <Annotatable> => Component::Annotatable(<>),
    <Annotation> => Component::Annotation(<>),
    <Factor> => Component::Factor(<>),
    "(" <Term> ")" => Component::Term(Box::new(<>)),
};

pub Annotation: Annotation<'input> = {
    "{" <s:r"[!-z|~ ]*\}"> => Annotation(s.trim_right_matches("}"))
};

pub Annotatable: Annotatable = {
    <SimpleUnit><Exponent> => Annotatable::UnitWithPower(<>),
    <SimpleUnit> => Annotatable::Unit(<>),
};

pub SimpleUnit: SimpleUnit = {
    <AtomSymbol> => SimpleUnit::Atom(<>),
    <PrefixSymbol><AtomSymbol> => SimpleUnit::PrefixedAtom(<>)
};

pub AtomSymbol: Atom = {
    <AtomByPrimaryCode>,
    <AtomBySecondaryCode>
};

AtomByPrimaryCode: Atom = {
    "m" => ATOMS[0].clone(),
    "s" => ATOMS[1].clone(),
    "g" => ATOMS[2].clone(),
};

AtomBySecondaryCode: Atom = {
    "M" => ATOMS[0].clone(),
    "S" => ATOMS[1].clone(),
    "G" => ATOMS[2].clone(),
};

pub PrefixSymbol: Prefix = {
    <PrefixByPrimaryCode>,
    <PrefixBySecondaryCode>
};

PrefixByPrimaryCode: Prefix = {
    "k" => PREFIXES[7].clone()
};

PrefixBySecondaryCode: Prefix = {
    "K" => PREFIXES[7].clone()
};

pub Exponent: Exponent = {
    <Sign><Digits> => Exponent(<>),
    <d:Digits> => Exponent(UnitSign::Positive, d)
};

Factor: Factor = {
    <Digits> => Factor(<>),
};

Digits: u32 = {
    <s:r"[1-9]+[0-9]*">  => u32::from_str(s).unwrap()
};

Sign: UnitSign = { PositiveSign, NegativeSign };
PositiveSign: UnitSign = <s:"+"> => UnitSign::Positive;
NegativeSign: UnitSign = <s:"-"> => UnitSign::Negative;

// vi:ft=rust
