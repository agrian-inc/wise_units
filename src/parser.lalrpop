use unit::Unit;
use unit::base::*;
use unit::derived::*;
use std::str::FromStr;
use parser_terms::*;
use prefix::{Prefix, PREFIXES};

grammar;

pub MainTerm: Term<'input> = {
    "/" <Term>,
    <Term>
};

pub Term: Term<'input> = {
    <c:Component> "." <t:Term> => Term::DotCombined(c, Box::new(t)),
    <c:Component> "/" <t:Term> => Term::SlashCombined(c, Box::new(t)),
    <Component> => Term::Basic(<>),
};

pub Component: Component<'input> = {
    <Annotatable><Annotation> => Component::AnnotatedAnnotatable(<>),
    <Annotatable> => Component::Annotatable(<>),
    <Annotation> => Component::Annotation(<>),
    <Factor> => Component::Factor(<>),
    "(" <Term> ")" => Component::Term(Box::new(<>)),
};

pub Annotation: Annotation<'input> = {
    "{" <s:r"[!-z|~]*\}"> => Annotation(s.trim_right_matches("}"))
};

pub Annotatable: Annotatable = {
    <SimpleUnit><Exponent> => Annotatable::UnitWithPower(<>),
    <SimpleUnit> => Annotatable::Unit(<>),
};

pub SimpleUnit: SimpleUnit = {
    <AtomSymbol> => SimpleUnit::Atom(<>),
    <PrefixSymbol><AtomSymbol> => SimpleUnit::PrefixedAtom(<>)
};

pub AtomSymbol: Box<Unit> = {
    <UnitByPrimaryCode>,
    <UnitBySecondaryCode>
};

UnitByPrimaryCode: Box<Unit> = {
    "m" => Box::new(Meter) as Box<Unit>,
    "s" => Box::new(Second) as Box<Unit>,
    "g" => Box::new(Gram) as Box<Unit>,
    "rad" => Box::new(Radian) as Box<Unit>,
    "K" => Box::new(Kelvin) as Box<Unit>,
    "C" => Box::new(Coulomb) as Box<Unit>,
    "cd" => Box::new(Candela) as Box<Unit>,

    "10^" => Box::new(TheNumberTenForArbitraryPowersCaret) as Box<Unit>,
    "10*" => Box::new(TheNumberTenForArbitraryPowersStar) as Box<Unit>,
    "[pi]" => Box::new(TheNumberPi) as Box<Unit>,
    "%" => Box::new(Percent) as Box<Unit>,
    "[ppth]" => Box::new(PartsPerThousand) as Box<Unit>,
    "[ppm]" => Box::new(PartsPerMillion) as Box<Unit>,
    "[ppb]" => Box::new(PartsPerBillion) as Box<Unit>,
};

UnitBySecondaryCode: Box<Unit> = {
    "M" => Box::new(Meter) as Box<Unit>,
    "S" => Box::new(Second) as Box<Unit>,
    "G" => Box::new(Gram) as Box<Unit>,
    "RAD" => Box::new(Radian) as Box<Unit>,
    "CD" => Box::new(Candela) as Box<Unit>,
    "[PI]" => Box::new(TheNumberPi) as Box<Unit>,
    "[PPTH]" => Box::new(PartsPerThousand) as Box<Unit>,
    "[PPM]" => Box::new(PartsPerMillion) as Box<Unit>,
    "[PPB]" => Box::new(PartsPerBillion) as Box<Unit>,
};

pub PrefixSymbol: Prefix = {
    <PrefixByPrimaryCode>,
    <PrefixBySecondaryCode>
};

PrefixByPrimaryCode: Prefix = {
    "k" => PREFIXES[7].clone()
};

PrefixBySecondaryCode: Prefix = {
    "K" => PREFIXES[7].clone()
};

pub Exponent: Exponent = {
    <Sign><Digits> => Exponent(<>),
    <d:Digits> => Exponent(UnitSign::Positive, d)
};

Factor: Factor = {
    <Digits> => Factor(<>),
};

Digits: u32 = {
    <s:r"[1-9]+[0-9]*">  => u32::from_str(s).unwrap()
};

Sign: UnitSign = { PositiveSign, NegativeSign };
PositiveSign: UnitSign = <s:"+"> => UnitSign::Positive;
NegativeSign: UnitSign = <s:"-"> => UnitSign::Negative;

// vi:ft=rust
