// <main-term>
main_term = ${ slash ~ term | term }

// <term>
term = {
    component ~ "." ~ term | component ~ slash ~ term | component
}

slash = { "/" }

// <component>
component = { factor ~ basic_component | basic_component }

basic_component = {
    annotatable ~ annotation |
        annotatable |
        annotation |
        factor |
        "(" ~ term ~ ")"
}

// <annotation>
annotation = { "{" ~ ('!'..'z')* ~ "}" }

// <annotatable>
annotatable = { simple_unit ~ exponent | simple_unit }

// <simple-unit>
simple_unit = {
    "10*"
        | "10^"
        | "1"
        | atom_char+
}

atom_char = _{
    "!" // 33
        | '#'..'\'' // 35-39
        | ","       // 44
        | ':'..'<'  // 58-60
        | '>'..'z'  // 62-90
        | "|"       // 124
        | "~"       // 126
}

exponent = { sign? ~ digits }
factor = { '0'..'9'+ }
digits = { '0'..'9'+ }
sign = { "-" | "+" }

// vim:ft=rust
